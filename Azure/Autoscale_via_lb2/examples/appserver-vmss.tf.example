# Backend VMSS - web server running DVWA

# Setup Onboarding scripts
locals {
  backendvm_custom_data = <<EOF
#!/bin/bash
apt-get update -y
apt-get install -y docker.io
docker run -d -p 80:80 --net=host --restart unless-stopped vulnerables/web-dvwa
EOF
}

# Create backend VM
resource "azurerm_linux_virtual_machine_scale_set" "backendvmss" {
  name                            = "backendvmss"
  location                        = azurerm_resource_group.main.location
  resource_group_name             = azurerm_resource_group.main.name
  sku                             = "Standard_B1ms"
  instances                       = 2
  admin_username                  = var.uname
  admin_password                  = var.upassword
  disable_password_authentication = false
  computer_name_prefix            = "backendvmss"
  custom_data                     = base64encode(local.backendvm_custom_data)

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "Premium_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "18.04-LTS"
    version   = "latest"
  }

  network_interface {
    name                      = "primary"
    primary                   = true
    network_security_group_id = azurerm_network_security_group.main.id

    ip_configuration {
      name      = "primary"
      primary   = true
      subnet_id = azurerm_subnet.External.id
    }
  }

  tags = {
    Name        = "${var.environment}-backend"
    environment = var.environment
    owner       = var.owner
    group       = var.group
    costcenter  = var.costcenter
  }

}
